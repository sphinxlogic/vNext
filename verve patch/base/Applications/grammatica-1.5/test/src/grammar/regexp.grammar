/*
 * regexp.grammar
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public License
 * as published by the Free Software Foundation; either version 3
 * of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free
 * Software Foundation, Inc., 59 Temple Place, Suite 330, Boston,
 * MA 02111-1307, USA.
 *
 * Copyright (c) 2003 Per Cederberg. All rights reserved.
 */

%header%

GRAMMARTYPE = "LL"

DESCRIPTION = "A grammar for simple regular expressions. The grammar is LL(1),
               meaning that it is best read with a recursive descent parser."

AUTHOR      = "Per Cederberg, <per at percederberg dot net>"
VERSION     = "1.0"
DATE        = "20 April 2003"

LICENSE     = "This library is free software; you can redistribute it and/or
               modify it under the terms of the GNU Lesser General Public License
               as published by the Free Software Foundation; either version 3
               of the License, or (at your option) any later version.

               This library is distributed in the hope that it will be useful,
               but WITHOUT ANY WARRANTY; without even the implied warranty of
               MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
               Lesser General Public License for more details.

               You should have received a copy of the GNU Lesser General Public
               License along with this library; if not, write to the Free
               Software Foundation, Inc., 59 Temple Place, Suite 330, Boston,
               MA 02111-1307, USA."

COPYRIGHT   = "Copyright (c) 2003 Per Cederberg. All rights reserved."


%tokens%

LEFT_PAREN                   = "("
RIGHT_PAREN                  = ")"
LEFT_BRACKET                 = "["
RIGHT_BRACKET                = "]"
LEFT_BRACE                   = "{"
RIGHT_BRACE                  = "}"
QUESTION                     = "?"
ASTERISK                     = "*"
PLUS                         = "+"
VERTICAL_BAR                 = "|"
DOT                          = "."
COMMA                        = ","
NUMBER                       = <<[0-9]+>>
CHAR                         = <<(\\.)|.>>


%productions%

Expr = Term ["|" Expr] ;

Term = Fact+ ;

Fact = Atom [AtomModifier] ;

Atom = CHAR
     | NUMBER
     | ","
     | "."
     | "(" Expr ")"
     | "[" CharacterSet "]" ;

AtomModifier = "?" ["?"]
             | "*" ["?"]
             | "+" ["?"]
             | "{" NUMBER ["," [NUMBER]] "}" ["?"] ;

CharacterSet = Character+ ;

Character = CHAR
          | NUMBER
          | ","
          | "."
          | "|"
          | "+"
          | "*"
          | "?"
          | "{"
          | "}"
          | "("
          | ")"
          | "[" ;
